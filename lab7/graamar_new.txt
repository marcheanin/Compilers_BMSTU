Program -> FuncDecl
            | Program FuncDecl .

FuncDecl -> FullTypeOrVoid Ident "<-" Parameters "=" Operators "."
            | FullTypeOrVoid Ident "=" Operators "." .

FullTypeOrVoid -> FullType
                  | KW_VOID .

Parameters -> Parameter | Parameters "," Parameter .
Parameter -> FullType VARNAME.

Operators -> Operator | Operators ";" Operator .
Operator -> DeclOperator
            | AssignOperator
            | FuncCallOperator
            | ChooseOperator
            | PredLoopOperator
            | PostLoopOperator
            | ForLoopOperator
            | EndFuncOperator .

DeclOperator -> FullType Decls .
Decls -> Decl
        | Decls "," Decl .
Decl -> Ident
        | Ident ":=" Expression .

AssignOperator -> DataExpression ":=" Expression .

ChooseOperator -> Expression "then" Operators "else" Operators "."
                  | Expression "then" Operators "." .

PredLoopOperator -> Expression "loop" Operators "." .
ForLoopOperator -> Expression "~" Expression "loop" Ident Operators "." .
PostLoopOperator -> "loop" Operators "while" Expression "." .

EndFuncOperator -> "return" Expression
                    | "return" .


OrOp -> "|" | "@" .
CmpOp -> ">" | "<" | "<=" | ">=" | "==" | "!=" .
AddOp -> "+" | "-" .
MulOp -> "*" | "//" | "%" .


Expression -> OrExpression .
OrExpression -> AndExpression |
                AndExpression OrOp AndExpression .

AndExpression -> CmpExpression |
                 CmpExpression "&" CmpExpression .

CmpExpression -> FuncCallExpression |
                 FuncCallExpression CmpOp FuncCallExpression .

FuncCallExpression -> ArithmExpression |
                        Ident "<-" NArithmExpressions .

ArithmExpressions -> ArithmExpression |
                      ArithmExpressions "," ArithmExpression .

ArithmExpression -> Term |
                    ArithmExpression AddOp Term .

Term -> Factor |
        Term MulOp Factor .

Factor -> Power |
          Power "^" Factor .

Power -> "!" Power |
          "-" Power |
          TYPE BaseExpression |
          DataExpression .

DataExpression -> BaseExpression |
                  DataExpression BaseExpression .

BaseExpression -> STRING_CONST | CHAR_CONST | INTEGER_CONST | Ident | "(" Expression ")" .


Ident -> "{" VARNAME "}" .
FullType -> TYPE
            | TYPE "[]"
            | TYPE "[][]" .